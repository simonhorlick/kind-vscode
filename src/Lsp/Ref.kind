type Lsp.Ref {
  new(
    // The source range.
    range: Pair(Nat, Nat),
    // The definition this term references.
    name: String,
  )
}

Lsp.Ref.find.go(term: Kind.Term, start: Nat, end: Nat)
  : List(Lsp.Ref)
  // Traverse the tree of Kind.Terms and collect any refs. If we come across
  // a Kind.Term.orig then we use that for the direct child.
  case term {
    ref: [
      Lsp.Ref.new(Pair.new!!(start, end), Kind.Name.show(term.name))
    ]
    mut: []
    imp: []
    var: []

    typ: []

    all:
      let type = Lsp.Ref.find.go(term.xtyp, 0, 0)
      let body = Lsp.Ref.find.go(
        term.body(
          Kind.Term.var(term.self, 0),
          Kind.Term.var(term.name, 0)
        ), 0, 0)
      List.concat!(type, body)

    lam: Lsp.Ref.find.go(term.body(Kind.Term.var(term.name, 0)), 0, 0)

    app:
        let func = Lsp.Ref.find.go(term.func, 0, 0)
        let argm = Lsp.Ref.find.go(term.argm, 0, 0)
        List.concat!(func, argm)

    let:
      let expr = Lsp.Ref.find.go(term.expr, 0, 0)
      let body = Lsp.Ref.find.go(
        term.body(Kind.Term.var(term.name, 0)),
        0, 0)
      List.concat!(expr, body)

    def:
      let expr = Lsp.Ref.find.go(term.expr, 0, 0)
      let body = Lsp.Ref.find.go(
        term.body(Kind.Term.var(term.name, 0)),
        0, 0)
      List.concat!(expr, body)

    ann:
      let term = Lsp.Ref.find.go(term.term, 0, 0)
      let type = Lsp.Ref.find.go(term.type, 0, 0)
      List.concat!(term, type)

    gol: []

    hol: []

    nat: []

    chr: []

    str: []

    cse:
      let wyth = List.flatten!(List.mapped!(term.with)!((defn)
        case defn {
          new:
            case defn.type {
              none: []
              some:
                let type = Lsp.Ref.find.go(defn.type.value, 0, 0)
                let term = Lsp.Ref.find.go(defn.term, 0, 0)
                List.concat!(term, type)
            }
        }))

      let cses = BitsMap.to_list!(term.cses);
      let cses = List.flatten!(List.mapped!(cses)!(
        (x)
          Lsp.Ref.find.go(Pair.snd!!(x), 0, 0)
      ));

      let moti = case term.moti {
        none: [],
        some: Lsp.Ref.find.go(term.moti.value, 0, 0),
      };

      let expr = Lsp.Ref.find.go(term.expr, 0, 0)
      List.concat!(wyth, List.concat!(cses, List.concat!(moti, expr)))

    ori:
      Lsp.Ref.find.go(
        term.expr,
        Pair.fst!!(term.orig),
        Pair.snd!!(term.orig))
    num: []
    new: []
    get: []
    set: []
    ope: []
  }

// find lists all of the names that the given Def references along with the
// source location of the name.
Lsp.Ref.find(d: Kind.Def): List(Lsp.Ref)
  open d
  List.concat!(
    Lsp.Ref.find.go(d.term, 0, 0),
    Lsp.Ref.find.go(d.type, 0, 0),
  )